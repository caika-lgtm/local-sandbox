binary := "target/debug/lsb"

# List available recipes
default:
    @just --list

# Build the guest init binary for the host platform by default.
# Set LSB_PLATFORM=<platform-id> to cross-build.
build-guest:
    LSB_PLATFORM="${LSB_PLATFORM:-}" cargo run -p xtask -- build-guest

# Build the guest kernel image via xtask.
# Set LSB_PLATFORM=<platform-id> to override the host default.
build-kernel:
    LSB_PLATFORM="${LSB_PLATFORM:-}" cargo run -p xtask -- build-kernel

# Build the CLI binary (debug)
build-cli:
    cargo build -p lsb-cli

# Codesign the CLI binary with the selected platform entitlement.
codesign:
    codesign --entitlements "$(LSB_PLATFORM="${LSB_PLATFORM:-}" cargo run -q -p xtask -- platform-meta --format env | sed -n 's/^LSB_CODESIGN_ENTITLEMENTS=//p')" --force -s - {{ binary }}

# Build everything: guest + CLI + codesign
build: build-guest build-cli codesign

# Prepare the rootfs, kernel, and initramfs (requires Docker).
# Set LSB_PLATFORM=<platform-id> to override the host default.
prepare-rootfs:
    LSB_PLATFORM="${LSB_PLATFORM:-}" cargo run -p xtask -- prepare-rootfs

# Run a command inside the VM
run *args:
    {{ binary }} run -- {{ args }}

# Open an interactive shell in the VM
shell:
    {{ binary }} run -- sh

# Full setup from scratch: rootfs + build
setup: prepare-rootfs build

# Check all crates compile (host targets only)
check:
    cargo check --workspace

# Run clippy on all crates
clippy:
    cargo clippy --workspace

# Install the binary to ~/.local/bin with codesign
install: build-guest
    cargo build -p lsb-cli --release
    codesign --entitlements "$(LSB_PLATFORM="${LSB_PLATFORM:-}" cargo run -q -p xtask -- platform-meta --format env | sed -n 's/^LSB_CODESIGN_ENTITLEMENTS=//p')" --force -s - target/release/lsb
    mkdir -p ~/.local/bin
    cp target/release/lsb ~/.local/bin/lsb

# Tag and push a release (triggers GitHub Actions)
release version:
    git tag -a "v{{ version }}" -m "Release v{{ version }}"
    git push origin "v{{ version }}"
